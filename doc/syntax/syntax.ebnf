% symbol is   <x>
% terminal is "x"
% produces is "="
% or is       "|"
% end is      ";"
% Separate everything with spaces

% ============================================================================
[[Program]]
% ============================================================================

program                     = <specification> ":" <program>
                            | <sequence>;


% ============================================================================
[[Specifications]]
% ============================================================================

specification               = <declaration>
                            | <abbreviation>
                            | <definition>
                            | <simultaneous-specification>;

simultaneous-specification  = {2 "&" <specification> };


[Declarations]

declaration                 = <specifier> {1 "," <name>};

specifier                   = <type>
                            | <type> <name>
                            | <type> <interface>
                            | <specifier> "[" "]" 
                            | <specifier> "[" <expression> "]";

type                        = "var"
                            | "chan"
                            | "call"
                            | "function"
                            | "process"
                            | "server";


[Abbreviations]

abbreviation                = <specifier> <name> "is" <element>
                            | <specifier> <name> "(" {0 "," <formal> } ")" "is" <element>
                            | "val" <name> "is" <expression>;


% ============================================================================
[[Sequence]] 
% ============================================================================

sequence                    = {0 ";" <command>};

command                     = <primitive-command> 
                            | <structured-command>
                            | <specification> ":" <command>;


[Primitive commands]                      

primitive-command           = <skip>
                            | <stop>
                            | <assignment>
                            | <input>
                            | <output>
                            | <connect>;

skip                        = "skip";
stop                        = "stop";
assignment                  = <element> ":=" <expression>;
input                       = <element> "?" <element>;
output                      = <element> "!" <expression>;
connect                     = "connect" <element> "to" <element>;
                         

[Structured commands]

structured-command          = <alternation>
                            | <conditional>
                            | <case>
                            | <loop> 
                            | "{" <sequence> "}"
                            | "{" <parallel> "}"
                            | "seq" {1 "," <range>} "do" <command>
                            | "par" {1 "," <range>} "do" <command>;

% Test                   
conditional                 = "test" "{" {0 "|" <choice> "}"
                            | "if" <expression> "do" <command>
                            | "if" <expression> "then" <command> "else" <command>;
choice                      = <guarded-choice>
                            | <conditional>
                            | <specification> ":" <choice>;
guarded-choice              = <expression> ":" <command>;
                         
% Case 
case                        = "case" "(" <expression> ")" "{" {0 "|" <selection>} "}";
selection                   = <expression> ":" <command>
                            | "else" <process>;

% Alternative
alternation                 = "alt" "{" {0 "|" <alternative>} "}";
alternative                 = <guarded-alternative>
                            | <alternation>
                            | <specification> ":" <alternative>;
guarded-alternative         = <guard> ":" <command>;
guard                       = <input>
                            | <expression> "&" <input>
                            | <expression> "&" <skip>;

% Loop 
loop                        = "while" <expression> "do" <command>
                            | "until" <expression> "do" <command>
                            | "do" <command> "while" <expr>
                            | "do" <command> "until" <expr>;


% ============================================================================
[[Parallel]] 
% ============================================================================

parallel                    = {0 "&" <component> };

component                   = <label> <process>
                            | <label> "[" {0 "," <process> } "]"
                            | <process>;

label                       = <name> "is";

process                     = <command>
                            | <interface> "to" <command>;

interface                   = "interface" "(" {0 "," <formal>} ")";


% ============================================================================
[[Server]]
% ============================================================================


[Specification]

server                      = <interface> "to" <declaration>
                            | <interface> "to" "{" {1 ":" <declaration> } "}";

declaration                 = "initial" <command>
                            | "final" <command>
                            | <alternation>;

guard                       = "accept" <name> "(" {0 "," <formal>} ")"
                            | <expression> "&" "accept" <name> "(" {0 "," <formal>} ")";

[Declarations]

declaration                 = "server" <name> "is" <server>
                            | "server" <name> "is" "[" {1 "," <server> } "]"
                            | "from" "{" {1 ":" <declaration>} "}" "interface" <name>;

[Call]

command                     = <element> "(" {0 "," <actual>} ")";


% ============================================================================
[[Replication]]
% ============================================================================

component                   = <label> <replicator> <process>;
conditional                 = "if" <replicator> <choice>;
case                        = "case" <replicator> <selection>;
alternation                 = "alt" <replicator> <alternative>;
declaration                 = "server" <name> "is" <replicator> <server>;

replicator                  = "[" {1 "," <range>} "]";
range                       = <name> "=" <expression> "for" <expression>
                            | <name> "=" <expression> "for" <expression> "step" <expression>;


% ============================================================================
[[Expressions]]
% ============================================================================

expression                  = <unary-operator> <operand>
                            | <operand> <binary-operator> <operand>
                            | <operand>;
operand                     = <element>
                            | <literal>
                            | "(" <expression> ")";

[Valof]
valof                       = "valof" <cmd> "result" <expression>
                            | <specification> ":" <valof>;
expression                  = "(" <valof> ")";


% ============================================================================
[[Elements]]
% ============================================================================

element                     = <element> "[" <expression> "]"
                            | <field>
                            | <name>;
field                       = <element> "." <name>;

literal                     = <decimal-integer>
                            | "#" <hexdecimal-integer>
                            | <byte>
                            | "true"
                            | "false";
hexdecimal-integer          = <hexdecimal-digit>
                            | <hexdecimal-digit> <hexdecimal-integer>;
decimal-integer             = <digit>
                            | <digit> <decimal-integer>;
byte                        = "'" <character> "'";


% ============================================================================
[[Abstraction]]
% ============================================================================

[Definitions]

definition                  = <procedure>
                            | <function>;

procedure                   = "process" <name> "(" {0 "," <formal>} ")" "is" <process>
                            | "server" <name> "(" {0 "," <formal>} ")" "is" <server>
                            | "server" <name> "(" {0 "," <formal>} ")" "inherits" <hiding-declaration>;

function                    = "function" <name> "(" {0 "," <formal>} ")" "is" <valof>
                            | "function" <name> "(" {0 "," <formal>} ")" "is" <expression>;

formal                      = <specifier> <name>
                            | <specifier> <name> "(" {0 "," <formal> } ")"
                            | "val" <name>;

[Instances]

process                     = <instance>;
server                      = <instance>;
command                     = <instance>;
expression                  = <instance>;
instance                    = <name> "(" {0 "," <actual>} ")";
actual                      = <element>
                            | <expression>;


% ============================================================================
[[Compiler transformations]]
% ============================================================================


[Process distribution]

process                     = "on" <expression> "do" <process>;


[Channel end references]

absolute-reference          = "(" <expression> ":" <expression> ":" <expression> ")";

% Interfaces
interface                   = "interface" "(" {0 "," <declaration>} ")" "@" <absolute-reference>;

% Calls
server-call                 = <local-chanend> "!" <element> "(" {0 "," <actual>} ")";
local-chanend               = <element> "@" <absolute-reference>;

% Connects
chanend                     = <remote-chanend>;
remote-chanend              = "@" <absolute-reference> "." <expression>;

